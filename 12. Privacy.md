# Privacy
_difficulty 8/10_

The creator of this contract was careful enough to protect the sensitive areas of its storage.  
Unlock this contract to beat the level.  
Things that might help:  
- Understanding how storage works  
- Understanding how parameter parsing works  
- Understanding how casting works  


Tips:  
- Remember that metamask is just a commodity. Use another tool if it is presenting problems. Advanced gameplay could involve using remix, or your own web3 provider.  


## Sources
```javascript
pragma solidity ^0.4.18;

contract Privacy {

  bool public locked = true;
  uint256 public constant ID = block.timestamp;
  uint8 private flattening = 10;
  uint8 private denomination = 255;
  uint16 private awkwardness = uint16(now);
  bytes32[3] private data;

  function Privacy(bytes32[3] _data) public {
    data = _data;
  }
  
  function unlock(bytes16 _key) public {
    require(_key == bytes16(data[2]));
    locked = false;
  }

  /*
    A bunch of super advanced solidity algorithms...

      ,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`
      .,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,
      *.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^         ,---/V\
      `*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.    ~|__(o.o)
      ^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'^`*.,*'  UU  UU
  */
}
```

## Level author  
Alejandro Santander  
palebluedot@gmail.com  
https://github.com/ajsantander  

## Solution

1. Open transaction on the `ropsten.etherscan.io`

2. Go to the "State Changes" tab and look at the data changed
```
0x000000000000000000000000000000000000000000000000000000c286ff0a01
0x859973068fce1f91dbc11565b4a325950c36e18211e339bb1c6d5c0a7af9e3fb
0xa47868d2b093ed1ea5d9c7a0098be2af316e2a7e5642a4ef0a83c87a23e5a1d7
0xb30aca730c04abca33bcf1bac726050b4f512ea0cad5395fd65002c54d037703
```

Looking at the contract code we can see several internal variables:  

```javascript
bool public locked = true;
uint256 public constant ID = block.timestamp;
uint8 private flattening = 10;
uint8 private denomination = 255;
uint16 private awkwardness = uint16(now);
bytes32[3] private data;
```

Variables share 256 bits if they can fit. In the first storage space we have variables `bool locked` = 0x01, `uint8 flattening` = 0x0a, `uint8 denomination` = 0xff, and `uint16 awkwardness` = 0xc286. Constants don't use this storage.

Next three spaces are `bytes32[3]`. We will take the last one (index 2) and use only the first half.

3. Invoke `unlock` method using this parameter.  
`await contract.unlock("0xb30aca730c04abca33bcf1bac726050b")`  

4. Check if contract is unlocked.  
`await contract.locked()`  
false  

5. Submit instance ðŸŽ‰  

## Lesson

Nothing in the ethereum blockchain is private. The keyword private is merely an artificial construct of the Solidity language. Web3's `getStorageAt(...)` can be used to read anything from storage. It can be tricky to read what you want though, since several optimization rules and techniques are used to compact the storage as much as possible.  


It can't get much more complicated than what was exposed in this level. For more, check out this excellent article by "Darius": [How to read Ethereum contract storage](https://medium.com/aigang-network/how-to-read-ethereum-contract-storage-44252c8af925).
